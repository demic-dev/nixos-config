{ ... }:
{
  flake.nixosModules.immich =
{ config, pkgs, env, ... }:
let
  fqdn = env.cloudSettings.fqdn;
  domain = "${env.cloudSettings.services.immich.subdomain}.${env.cloudSettings.fqdn}";
  port = env.cloudSettings.services.immich.port;
  redisPort = port + 1;

  immichPath = "/data/immich";
in
{
  services.immich = {
    enable = true;
    package = pkgs.immich;

    port = port;
    host = "127.0.0.1";

    mediaLocation = "/persist${immichPath}";

    database = {
      enable = true;

      createDB = true;
      user = "immich";
      name = "immich";
      host = "/run/postgresql";
    };

    redis = {
      enable = true;

      host = "127.0.0.1";
      port = redisPort;
    };

    settings = {
      server.externalDomain = "https://${domain}";
      backup.database.enabled = true;
    };

  };

  # Postgres
  services.postgresql = {
    ensureDatabases = [ "immich" ];
    ensureUsers = [
      {
        name = "immich";
        ensureDBOwnership = true;
      }
    ];
  };

  # Redis
  services.redis.servers.immich = {
    enable = true;
    port = redisPort;
    bind = "127.0.0.1";
  };

  # Shouldn't be necessary anymore since it's under /persist/data
  environment.persistence."/persist".directories = [
    {
      directory = immichPath;
      user = "immich";
      group = "immich";
    }
  ];

  # Fail2Ban Jail
   services.fail2ban.jails.immich.settings = {
    filter = "immich";
    backend = "systemd";
    findtime = 86400;
    bantime  = 43200;
    maxretry = 5;
    chain = "FORWARD";
  };

  environment.etc."fail2ban/filter.d/immich.local".text = pkgs.lib.mkDefault( pkgs.lib.mkAfter ''
    [Definition]
    failregex = immich-server.*Failed login attempt for user.+from ip address\s?<ADDR>
    journalmatch = CONTAINER_TAG=immich-server
  '');

  # Nginx
  services.nginx.virtualHosts.${domain} = {
    serverName = domain;

    enableACME = false;
    useACMEHost = fqdn;
    forceSSL = true;

    locations."/" = {
      recommendedProxySettings = true;
      proxyPass = "http://localhost:${builtins.toString port}";
      proxyWebsockets = true;
      extraConfig = ''
        proxy_set_header Host $host; 
        proxy_set_header X-Forwarded-Proto $scheme; 
        proxy_set_header X-Real-IP $remote_addr; 
        proxy_set_header X-Forwarded-For $proxy_add_x_forwarded_for;
        proxy_max_temp_file_size 16384m; 

        client_max_body_size 100m;
      '';
    };
  };

  services.borgbackup.jobs."immich-backup-borgbase" = {
    paths = [ immichPath ];

    repo = env.cloudSettings.services.immich.borg-repository;
    preHook = ''
      ${pkgs.zfs}/bin/zfs destroy rpool/safe/persist@borgbase && true
      ${pkgs.zfs}/bin/zfs snapshot rpool/safe/persist@borgbase
      /run/current-system/sw/bin/mkdir -p /var/tmp/borgjobs
      /run/wrappers/bin/mount --bind /persist/.zfs/snapshot/borgbase /var/tmp/borgjobs/
    '';
    postHook = ''
      /run/wrappers/bin/umount /var/tmp/borgjobs/
      ${pkgs.zfs}/bin/zfs destroy rpool/safe/persist@borgbase
    '';
    encryption = {
      mode = "repokey-blake2";
      passCommand = "cat /run/agenix/immich-backup_passphrase";
    };
    environment.BORG_RSH = "ssh -i /home/michele/.ssh/id_ed25519";
    compression = "auto,lzma";
    startAt = "daily";

    user = "root";
    group = "root";
  };

  # Agenix
  age.secrets = {
    immich-backup_passphrase = {
      file = ../../../secrets/immich-backup_passphrase.age;
    };
  };

  users.users.immich.uid = 15015;
  users.groups.immich.gid = 15015;

  users.users.redis-immich.uid = 995;
  users.groups.redis-immich.gid = 995;
}
  ;
}
